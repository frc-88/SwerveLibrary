buildscript {
    if (rootProject == project) {
        repositories {
            maven {
                url 'https://plugins.gradle.org/m2/'
                url 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            }
            jcenter()
        }
        dependencies {
            classpath "edu.wpi.first:GradleRIO:2021.3.1"
            classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
            classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
        }
    }
}

plugins {
    // Support for the java library
    id 'java-library'
    // Code formatter
    id 'com.github.sherter.google-java-format' version '0.9'
}

apply plugin: 'edu.wpi.first.GradleRIO'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.codearte.nexus-staging'

archivesBaseName = 'swerve'
group = "io.github.frc-88"
version = "0.1.0-rc.0"

compileJava.dependsOn 'googleJavaFormat'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

dependencies {
    compile wpi.deps.wpilib()
    compile wpi.deps.vendor.java()

    // Tuples library
    api "org.javatuples:javatuples:1.2"

    // Apache Commons Math (for linear algebra)
    api "org.apache.commons:commons-math3:3.6.1"

    // TOML Config Parsing
    api "com.electronwill.night-config:toml:3.6.3"

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

    // Mockito for testing
    testImplementation 'org.mockito:mockito-core:3.2.4'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

modifyPom {
    project {
        name 'swerve'
        description 'Controls library for swerve drives, including differential swerves.'
        url 'https://github.com/frc-88/SwerveLibrary'
        inceptionYear '2021'

        scm {
            url 'https://github.com/frc-88/SwerveLibrary'
            connection 'scm:https://github.com/frc-88/SwerveLibrary'
            developerConnection 'scm:git://github.com/frc-88/SwerveLibrary'
        }

        licenses {
            license {
                name 'The MIT License'
                url 'https://github.com/frc-88/SwerveLibrary/blob/master/LICENSE.md'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'paulterrasi'
                name 'Paul Terrasi'
                email 'paul.a.terrasi@gmail.com'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = true
    javadoc = true
}

nexus {
    sign = true
    repositoryUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    snapshotRepositoryUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
}

nexusStaging {
    serverUrl = "https://s01.oss.sonatype.org/service/local/"
    packageGroup = "io.github.frc-88"
}
